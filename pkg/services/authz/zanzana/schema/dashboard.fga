module dashboard

extend type org
  relations

    define dashboard_create: [role#assignee] or fixed_folders_writer or fixed_dashboards_creator
    define dashboard_read: [role#assignee] or fixed_folders_reader or fixed_folders_writer or fixed_dashboards_reader
    define dashboard_write: [role#assignee] or fixed_folders_writer or fixed_dashboards_writer
    define dashboard_delete: [role#assignee] or fixed_folders_writer or fixed_dashboards_writer
    define dashboard_permissions_read: [role#assignee] or fixed_folders_writer or fixed_dashboards_permissions_reader
    define dashboard_permissions_write: [role#assignee] or fixed_folders_writer or fixed_dashboards_permissions_writer
    define dashboard_public_write: [role#assignee] or dashboard_write or fixed_dashboards_public_writer

    define dashboard_annotations_create: [role#assignee]
    define dashboard_annotations_read: [role#assignee]
    define dashboard_annotations_write: [role#assignee]
    define dashboard_annotations_delete: [role#assignee]

    # Fixed roles
    define fixed_dashboards_creator: [user, team#member, role#assignee] or fixed_dashboards_writer
    define fixed_dashboards_reader: [user, team#member, role#assignee] or fixed_dashboards_writer
    define fixed_dashboards_writer: [user, team#member, role#assignee]
    define fixed_dashboards_insights_reader: [user, team#member, role#assignee]
    define fixed_dashboards_public_writer: [user, team#member, role#assignee]
    define fixed_dashboards_permissions_reader: [user, team#member, role#assignee] or fixed_dashboards_permissions_writer
    define fixed_dashboards_permissions_writer: [user, team#member, role#assignee]

type dashboard
  relations
    define org: [org]

    define read: [user, team#member, role#assignee] or dashboard_read from org
    define write: [user, team#member, role#assignee] or dashboard_write from org
    define delete: [user, team#member, role#assignee] or dashboard_delete from org
    define create: [user, team#member, role#assignee] or dashboard_create from org
    define permissions_read: [user, team#member, role#assignee] or dashboard_permissions_read from org
    define permissions_write: [user, team#member, role#assignee] or dashboard_permissions_write from org

    define public_write: [user, team#member, role#assignee] or dashboard_public_write from org or write
    define annotations_create: [user, team#member, role#assignee] or dashboard_annotations_create from org
    define annotations_read: [user, team#member, role#assignee] or dashboard_annotations_read from org
    define annotations_write: [user, team#member, role#assignee] or dashboard_annotations_write from org
    define annotations_delete: [user, team#member, role#assignee] or dashboard_annotations_delete from org

