@top AlertRuleSearch { expression+ }

@dialects { dataSourceFilter, nameSpaceFilter, labelFilter, groupFilter, ruleFilter, stateFilter, typeFilter, healthFilter, dashboardFilter, pluginsFilter, contactPointFilter, sourceFilter }

expression { (FilterExpression | FreeFormExpression) expression }

FreeFormExpression { word (colon word)* | stringWithQuotes }

FilterExpression {
 filter<DataSourceToken> |
 filter<NameSpaceToken> |
 filter<LabelToken> |
 filter<GroupToken> |
 filter<RuleToken> |
 filter<StateToken> |
 filter<TypeToken> |
 filter<HealthToken> |
 filter<DashboardToken> |
 filter<PluginsToken> |
 filter<ContactPointToken> |
 filter<RuleSourceToken>
}

filter<token> { token FilterValue }

@tokens {
  colon { ":" }

  // Special characters (except colon, quotes and space), Latin characters, extended latin and emoji
  allowedInputChar { $[!#$%&'()*+,-./] | $[\u{0030}-\u{0039}] | $[\u{003b}-\u{1eff}] | $[\u{2030}-\u{1faff}] }
  word { allowedInputChar+ }

  allowedInputCharOrColon { allowedInputChar | colon }
  allowedInputCharOrColonOrWhitespace { allowedInputCharOrColon | @whitespace }
  stringWithQuotes { ("\"" allowedInputCharOrColonOrWhitespace+ "\"") }

  FilterValue { allowedInputCharOrColon+ | stringWithQuotes }
  filterToken<type> { type colon }

  DataSourceToken[@dialect=dataSourceFilter] { filterToken<"datasource"> }
  NameSpaceToken[@dialect=nameSpaceFilter] { filterToken<"namespace"> }
  LabelToken[@dialect=labelFilter] { filterToken<"label"> }
  GroupToken[@dialect=groupFilter] { filterToken<"group"> }
  RuleToken[@dialect=ruleFilter] { filterToken<"rule"> }
  StateToken[@dialect=stateFilter] { filterToken<"state"> }
  TypeToken[@dialect=typeFilter] { filterToken<"type"> }
  HealthToken[@dialect=healthFilter] { filterToken<"health"> }
  DashboardToken[@dialect=dashboardFilter] { filterToken<"dashboard"> }
  PluginsToken[@dialect=pluginsFilter] { filterToken<"plugins"> }
  ContactPointToken[@dialect=contactPointFilter] { filterToken<"contactPoint"> }
  RuleSourceToken[@dialect=sourceFilter] { filterToken<"source"> }

  @precedence { DataSourceToken, word }
  @precedence { NameSpaceToken, word }
  @precedence { LabelToken, word }
  @precedence { GroupToken, word }
  @precedence { RuleToken, word }
  @precedence { StateToken, word }
  @precedence { TypeToken, word }
  @precedence { HealthToken, word }
  @precedence { DashboardToken, word }
  @precedence { PluginsToken, word }
  @precedence { ContactPointToken, word }
  @precedence { RuleSourceToken, word }
}

